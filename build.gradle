plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = project.pacprofile_group
archivesBaseName = project.pacprofile_name
version = project.pacprofile_version

repositories {
    // LuckPerms
    mavenCentral()
    // Paper
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    // AnvilGUI
    maven { url "https://repo.codemc.io/repository/maven-snapshots/" }
    // Essentials
    maven { url "https://repo.essentialsx.net/releases/" }
    // GriefPrevention
    maven { url "https://jitpack.io" }

    flatDir { dirs 'libs' }
}

configurations {
    include.canBeResolved = true
    compileOnly.extendsFrom(include)
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paper_version}"

    include "net.wesjd:anvilgui:${anvilgui_version}"

    compileOnly "net.luckperms:api:${luckperms_version}"
    compileOnly "net.essentialsx:EssentialsX:${essentials_version}"
    compileOnly "com.github.TechFortress:GriefPrevention:${griefprevention_version}"

    compileOnly name: "pacutilities-${pacutilities_version}"
    compileOnly name: "pacbirthday-${pacbirthday_version}"
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    configurations = [project.configurations.include]

    archiveFileName = "${archivesBaseName}-${version}.jar"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

processResources {
    filesMatching('plugin.yml') {
        expand(version: version)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/PeaceAndCubeMC/PACProfile"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
