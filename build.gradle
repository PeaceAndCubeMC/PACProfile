import java.nio.charset.StandardCharsets

plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = project.pacprofile_group
version = project.pacprofile_version

repositories {
    // LuckPerms
    mavenCentral()
    // Paper
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Essentials
    maven { url = "https://repo.essentialsx.net/releases/" }
    // GriefPrevention
    maven { url = "https://jitpack.io" }

    flatDir { dirs 'libs' }
}

configurations {
    include.canBeResolved = true
    compileOnly.extendsFrom(include)
}

dependencies {
    implementation "io.papermc.paper:paper-api:${paper_version}"

    compileOnly "net.luckperms:api:${luckperms_version}"
    compileOnly "net.essentialsx:EssentialsX:${essentials_version}"
    compileOnly "com.github.TechFortress:GriefPrevention:${griefprevention_version}"

    compileOnly name: "pacutilities-${pacutilities_version}"
    compileOnly name: "pacbirthday-${pacbirthday_version}"

    testImplementation "org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.79.0"
    testImplementation "org.junit.jupiter:junit-jupiter:5.13.4"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.13.4"
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    configurations = [project.configurations.include]

    archiveFileName = "${pacprofile_name}-${version}.jar"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

tasks {
    compileJava {
        options.release = 21
        options.encoding = StandardCharsets.UTF_8.name()
    }

    checkstyle {
        configFile = rootProject.file("checkstyle.xml")
        toolVersion = "10.21.4"
    }

    test {
        useJUnitPlatform()
    }
}

processResources {
    filesMatching('plugin.yml') {
        expand(version: version)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/PeaceAndCubeMC/PACProfile"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
